plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenLocal()
}

loom {
    splitEnvironmentSourceSets()

    runs {
        server {
            runDir "run"
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation "de.gnm:mcdash-api:${project.mcdash_version}"
    shadow "de.gnm:mcdash-api:${project.mcdash_version}"
}

tasks.register('buildWebUI', Exec) {
    description = 'Builds the web UI using pnpm'
    workingDir = file("${projectDir}/../../ui")
    
    commandLine 'sh', '-c', 'pnpm install && pnpm run build'

    inputs.dir("${projectDir}/../../ui/src")
    inputs.file("${projectDir}/../../ui/package.json")
    inputs.file("${projectDir}/../../ui/vite.config.ts")
    outputs.dir("${projectDir}/../../ui/dist")
}

tasks.register('copyWebUI', Copy) {
    description = 'Copies the built web UI to resources'
    dependsOn 'buildWebUI'
    
    from "${projectDir}/../../ui/dist"
    into "${projectDir}/src/main/resources/webui"
}

tasks.named('clean') {
    doLast {
        delete "${projectDir}/src/main/resources/webui"
    }
}

processResources {
    dependsOn 'copyWebUI'
    
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.matching { it.name == 'sourcesJar' }.configureEach {
    dependsOn 'copyWebUI'
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}

tasks.named("shadowJar") {
    configurations = [project.configurations.shadow]
    archiveClassifier.set("dev-all")

    relocate 'io.undertow', 'de.gnm.mcdash.libs.undertow'
}

tasks.named("remapJar") {
    dependsOn("shadowJar")
    inputFile = tasks.shadowJar.archiveFile
    archiveClassifier.set("")
}

tasks.build {
    dependsOn("remapJar")
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
